// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs.
 * Which function to compute is determined by 6 input bits 
 * denoted zx, nx, zy, ny, f, no.
 * The computed function's value is called "out".
 * In addition to computing out, the ALU computes two 
 * 1-bit outputs called zr and ng:
 * if out == 0, zr = 1; otherwise zr = 0;
 * If out < 0, ng = 1; otherwise ng = 0.
 * The 6-bit combinations (zx,nx,zy,ny,f,no) and 
 * their effect are documented in the book. 
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx  == 1) sets x = 0        // 16-bit constant
// if (nx  == 1) sets x = ~x       // bitwise "not"
// if (zy  == 1) sets y = 0        // 16-bit constant
// if (ny  == 1) sets y = ~y       // bitwise "not"
// if (f   == 1) sets out = x + y  // integer 2's-complement addition
// if (f   == 0) sets out = x & y  // bitwise And
// if (no  == 1) sets out = ~out   // bitwise Not
// if (out == 0) sets zr = 1
// if (out < 0)  sets ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if f == 1) or out = x & y (if == 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:

    // process x
    Mux16(a[0..15]=x[0..15], b[0..15]=false, sel=zx, out=maybezeroedx);
    Not16(in[0..15]=maybezeroedx, out=negatedx);
    Mux16(a[0..15]=maybezeroedx, b[0..15]=negatedx, sel=nx, out=processedx);

    // process y
    Mux16(a[0..15]=y[0..15], b[0..15]=false, sel=zy, out=maybezeroedy);
    Not16(in[0..15]=maybezeroedy, out=negatedy);
    Mux16(a[0..15]=maybezeroedy, b[0..15]=negatedy, sel=ny, out=processedy);

    //Not16(in[0..15]=y[0..15], out=noty);
    //Mux16(a[0..15]=y[0..15], b[0..15]=noty, sel=ny, out=maybenegatedy);
    //Mux16(a=maybenegatedy, b[0..15]=false, sel=zy, out=processedy);

    // compute add or and
    Add16(a[0..15]=processedx, b[0..15]=processedy, out=xplusy);
    And16(a[0..15]=processedx, b[0..15]=processedy, out=xandy);

    // select function result
    Mux16(a[0..15]=xandy, b[0..15]=xplusy, sel=f, out=fout);

    // negate function out
    Not16(in[0..15]=fout, out=negatedout);

    // select ouput
    Mux16(a[0..15]=fout, b[0..15]=negatedout, sel=no, out=finalout);

    // passthrough finalout to global output
    Not16(in[0..15]=finalout, out[0..15]=passthrough1);
    Not16(in[0..15]=passthrough1, out=out);

    // zero flag
    Or16Way(in[0..15]=finalout, out=notzero);
    Not(in=notzero, out=zr); 

    // negative flag
    IsNegative(in[0..15]=finalout, out=ng);
}
